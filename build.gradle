
plugins {
	id 'java'
	id "com.diffplug.spotless" version "6.20.0"
	id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

ext {
	prodJavaVersion = 21
	jdkVersion = 22
}

repositories {
	maven {
		url 'https://plugins.gradle.org/m2/'
	}
	mavenCentral()
}

spotless {
	enforceCheck false
	java {
		importOrder '', '\\#' // Static imports after regular imports, as per mt-server standard and IntelliJ default
		removeUnusedImports()
		trimTrailingWhitespace()
		indentWithTabs()
		replaceRegex 'class-level javadoc indentation fix', /^\*/, ' *'
		replaceRegex 'method-level javadoc indentation fix', /\t\*/, '\t *'
	}
	format 'misc', {
		target '**/*.gradle', '**/*.md', '**/.gitignore'

		trimTrailingWhitespace()
		//indentWithTabs() For markdown, we want to align some code examples very carefully, so we use spaces
		replaceRegex 'class-level javadoc indentation fix', /^\*/, ' *'
		replaceRegex 'method-level javadoc indentation fix', /\t\*/, '\t *'
		endWithNewline()
	}
}

allprojects {
	group = 'works.bosk'
	// version should be set as a property with -Pversion
}

nexusPublishing {
	repositories {
		sonatype {
			nexusUrl = uri('https://s01.oss.sonatype.org/service/local/')
			snapshotRepositoryUrl = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
			username = project.properties['ossrhUsername'] ?: 'bogusUser'
			password = project.properties['ossrhPassword'] ?: 'bogusPassword'
		}
	}
}
